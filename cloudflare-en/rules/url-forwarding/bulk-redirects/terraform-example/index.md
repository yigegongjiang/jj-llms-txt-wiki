---
description: Create Bulk Redirects using the Terraform Cloudflare provider.
title: Configure Bulk Redirects using Terraform
image: https://developers.cloudflare.com/og-docs.png
---

[Skip to content](#main-content)

> Documentation Index  
> Fetch the complete documentation index at: https://developers.cloudflare.com/rules/llms.txt  
> Use this file to discover all available pages before exploring further.

# Configure Bulk Redirects using Terraform

Last updated May 5, 2026|Copy as Markdown|[View as Markdown](https://developers.cloudflare.com/rules/url-forwarding/bulk-redirects/terraform-example/index.md)|[Agent setup](https://developers.cloudflare.com/agent-setup/)

Note

Terraform code snippets below refer to the v4 SDK only.

This Terraform example configures account-level Bulk Redirects. It creates a [Bulk Redirect List](https://developers.cloudflare.com/rules/url-forwarding/bulk-redirects/concepts/#bulk-redirect-lists) populated with [URL redirects](https://developers.cloudflare.com/rules/url-forwarding/bulk-redirects/concepts/#url-redirects) and a corresponding [Bulk Redirect Rule](https://developers.cloudflare.com/rules/url-forwarding/bulk-redirects/concepts/#bulk-redirect-rules) to activate them.

```tf
# Cloudflare account ID
variable "cloudflare_account_id" {
  default = "<ACCOUNT_ID>"
}

# Bulk redirect list description
variable "bulk_redirect_list_description" {
  default = "my bulk redirect description"
}

# Bulk redirect list name
variable "bulk_redirect_list_name" {
  default = "my_bulk_redirect_list_name"
}

# Bulk redirect list item (URL redirect)
variable "bulk_redirects" {
  type = map(object({
    source_url  = string
    target_url  = string
    status_code = number
  }))

  default = {
    "redirect1" = {
      source_url = "https://source.url/redirect/1"
      target_url = "https://target.url/?redirect=1"
      status_code = 301
    }
    "redirect2" = {
      source_url = "https://source.url/redirect/2"
      target_url = "https://target.url/?redirect=2"
      status_code = 302
    }
    "redirect3" = {
      source_url = "https://source.url/redirect/3"
      target_url = "https://target.url/?redirect=3"
      status_code = 307
    }
  }
}

# Create redirect list
resource "cloudflare_list" "bulk_redirect_to_id" {
  account_id  = var.cloudflare_account_id
  name        = var.bulk_redirect_list_name
  description = var.bulk_redirect_list_description
  kind        = "redirect"
}

# Add redirect item into the redirect list
resource "cloudflare_list_item" "bulk_redirect_to_id_item" {
  for_each = { for redirect in var.bulk_redirects : "${redirect.source_url}" => redirect }

  account_id = var.cloudflare_account_id
  list_id    = cloudflare_list.bulk_redirect_to_id.id

  redirect {
    source_url  = each.value.source_url
    target_url  = each.value.target_url
    status_code = each.value.status_code
  }

  depends_on = [
    cloudflare_list.bulk_redirect_to_id
  ]

}

# Create bulk redirect and attach redirect list
resource "cloudflare_ruleset" "bulk_root_redirect_to_id" {
  account_id  = var.cloudflare_account_id
  name        = var.bulk_redirect_list_name
  description = var.bulk_redirect_list_description
  kind        = "root"
  phase       = "http_request_redirect"

  rules {
    action = "redirect"
    action_parameters {
      from_list {
        name = var.bulk_redirect_list_name
        key  = "http.request.full_uri"
      }
    }
    expression  = "http.request.full_uri in ${"$"}${var.bulk_redirect_list_name}"
    description = var.bulk_redirect_list_description
    enabled     = true
  }

  depends_on = [
    cloudflare_list_item.bulk_redirect_to_id_item
  ]
}
```

## Required token permissions

Your API token must have at least the following [permissions](https://developers.cloudflare.com/fundamentals/api/reference/permissions/):

* Account Filter Lists > Edit
* Bulk URL Redirects > Edit

* Account Rule Lists Write
* Bulk URL Redirects Write

## Additional resources

For additional guidance on using Terraform with Cloudflare, refer to the following resources:

* [Terraform documentation](https://developers.cloudflare.com/terraform/)
* [Cloudflare Provider for Terraform ↗](https://registry.terraform.io/providers/cloudflare/cloudflare/latest/docs) (reference documentation)

Was this helpful?

YesNo

## On this page

[![](https://developers.cloudflare.com/_astro/logo.te5VL_aD.svg)Docs](https://developers.cloudflare.com/)

```json
{"@context":"https://schema.org","@type":"TechArticle","@id":"https://developers.cloudflare.com/rules/url-forwarding/bulk-redirects/terraform-example/#page","headline":"Configure Bulk Redirects using Terraform · Cloudflare Rules docs","description":"Create Bulk Redirects using the Terraform Cloudflare provider.","url":"https://developers.cloudflare.com/rules/url-forwarding/bulk-redirects/terraform-example/","inLanguage":"en","image":"https://developers.cloudflare.com/og-docs.png","dateModified":"2026-05-05","publisher":{"@type":"Organization","name":"Cloudflare","description":"One platform for your apps, agents, and workforce. Build, secure, and scale without managing infrastructure","url":"https://www.cloudflare.com/","sameAs":["https://github.com/cloudflare","https://www.linkedin.com/company/cloudflare","https://x.com/cloudflare"],"logo":{"@type":"ImageObject","url":"https://developers.cloudflare.com/logo.svg"},"address":{"@type":"PostalAddress","streetAddress":"101 Townsend St","addressLocality":"San Francisco","addressRegion":"CA","postalCode":"94107","addressCountry":"US"},"contactPoint":[{"@type":"ContactPoint","contactType":"Customer Support","url":"https://support.cloudflare.com/","availableLanguage":["English"]},{"@type":"ContactPoint","contactType":"Sales","url":"https://www.cloudflare.com/contact/","availableLanguage":["English"]}]},"isPartOf":{"@type":"WebSite","@id":"https://developers.cloudflare.com/#website","name":"Cloudflare Docs","url":"https://developers.cloudflare.com/"},"keywords":["Terraform","Redirects"]}
```
